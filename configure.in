dnl $Id: configure.in,v 1.6 1998/12/11 22:39:20 sbooth Exp $

dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/Cgicc.cc)
AM_INIT_AUTOMAKE(Cgicc, 2.2.2)
AM_CONFIG_HEADER(src/config.h)

AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL
AM_PROG_LIBTOOL

dnl Checks for libraries.

dnl setup for networking functions
AC_CHECK_LIB(nsl, gethostbyaddr, LIB_NET="-lnsl")
AC_SUBST(LIB_NET)

dnl check whether to link against libstdc++
CGICC_CHECK_LINK_STDCPP

dnl Checks for header files.
AC_CHECK_HEADERS(sys/time.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM

dnl verify exception handling works
CGICC_CHECK_CPP_EXCEPTIONS

dnl verify templates work
CGICC_CHECK_CPP_TEMPLATES

dnl Checks for library functions.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_FUNCS(strftime gettimeofday uname)

dnl enable/disable the tests, if specified
AC_ARG_ENABLE(tests, 
	[  --enable-tests          build tests [default=yes]],
	[case "${enableval}" in
		yes) tests=true ;;
		no)  tests=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-tests) ;;
		esac], 
	tests=true)
AM_CONDITIONAL(TESTS, test "$tests" = true)

dnl enable/disable the documentation, if specified
AC_ARG_ENABLE(doc, 
	[  --enable-doc            include documentation [default=yes]],
	[case "${enableval}" in
		yes) doc=true ;;
		no)  doc=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-doc) ;;
		esac], 
	doc=true)
AM_CONDITIONAL(DOCS, test "$doc" = true)


AC_OUTPUT([Makefile src/Makefile src/Cgicc.hh test/Makefile doc/Makefile])

