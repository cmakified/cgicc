dnl $Id: configure.in,v 1.9 1999/05/05 18:30:54 sbooth Exp $

dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/Cgicc.cc)
AM_INIT_AUTOMAKE(cgicc, 3.0)
AM_CONFIG_HEADER(src/config.h)

AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL
AM_PROG_LIBTOOL

dnl Checks for libraries.

dnl setup for networking functions
AC_CHECK_LIB(nsl, gethostbyaddr, LIB_NET="-lnsl")
AC_SUBST(LIB_NET)

dnl check whether to link against libstdc++
CGICC_CHECK_LINK_STDCPP

dnl Checks for header files.
AC_CHECK_HEADERS(sys/time.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM

dnl Determine host system type
AC_CANONICAL_HOST
AC_DEFINE_UNQUOTED(HOST, "$host")

dnl verify exception handling works
CGICC_CHECK_CPP_EXCEPTIONS

dnl verify templates work
CGICC_CHECK_CPP_TEMPLATES

dnl verify STL support
CGICC_CHECK_CPP_STL

dnl verify namespace support
CGICC_CHECK_CPP_NAMESPACES

dnl Checks for library functions.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_FUNCS(strftime gettimeofday uname)

dnl enable/disable the demos, if specified
AC_ARG_ENABLE(demos, 
	[  --enable-demos          build demos [default=yes]],
	[case "${enableval}" in
		yes) demos=true ;;
		no)  demos=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-demos) ;;
		esac], 
	demos=true)
AM_CONDITIONAL(DEMOS, test "$demos" = true)

dnl enable/disable namespaces, if specified
AC_ARG_ENABLE(namespaces,
	[  --enable-namespaces     enable namespace support [default=yes]],
	[case "${enableval}" in
		yes) namespaces=true ;;
		no)  namespaces=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-namespaces) ;;
		esac], 
	namespaces=true)
if (test "$namespaces" = true && test "$cgicc_cv_cpp_namespaces" = yes); then
	AC_DEFINE(USE_NAMESPACES, 1)
else
	AC_DEFINE(USE_NAMESPACES, 0)
fi

AC_OUTPUT([Makefile src/Makefile info/Makefile demo/Makefile example/Makefile])

